PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX alpaca: <http://purl.org/alpaca#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX neao_data: <http://purl.org/neao/data#>
PREFIX neao_steps: <http://purl.org/neao/steps#>
PREFIX neao_base: <http://purl.org/neao/base#>

SELECT DISTINCT ?file_path ?package_name ?package_version  WHERE {
	?output_file rdf:type alpaca:FileEntity .
    ?output_file (prov:wasDerivedFrom|^prov:hadMember)* ?data .
    ?data rdf:type neao_data:PowerSpectralDensity .

	?function_execution neao_base:hasOutput ?data .
    ?function_execution rdf:type neao_steps:PowerSpectralDensityAnalysis .

    ?function_execution neao_base:isImplementedIn/neao_base:isImplementedInPackage ?package .
    ?package neao_base:packageName ?package_name .
    ?package neao_base:packageVersion ?package_version .

    ?output_file alpaca:filePath ?file_path .
}
ORDER BY ?file_path