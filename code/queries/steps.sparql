PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX alpaca: <http://purl.org/alpaca#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX neao_data: <http://purl.org/neao/data#>
PREFIX neao_steps: <http://purl.org/neao/steps#>
PREFIX neao_base: <http://purl.org/neao/base#>

SELECT DISTINCT ?file_path ?neao_class WHERE {

    # Select all data entities that were saved in each file
    {
        SELECT DISTINCT ?output_file ?data ?script WHERE {
        	?output_file rdf:type alpaca:FileEntity .
        	?output_file prov:wasDerivedFrom/prov:wasDerivedFrom ?data .
        	?data rdf:type neao_base:Data .
            ?output_file prov:wasAttributedTo ?script .
    	}
    }

    # Identify all function executions leading to saved data
    ?data (prov:wasDerivedFrom|^prov:hadMember)* ?input_data .
    ?function neao_base:hasOutput ?input_data .
    ?function prov:wasAssociatedWith ?script .

    # Get class name
    ?function rdf:type ?neao_class .
    ?neao_class rdfs:subClassOf neao_base:AnalysisStep .
    FILTER (?neao_class != neao_base:AnalysisStep) .
    FILTER (CONTAINS(STR(?neao_class), "Compute") ||
            CONTAINS(STR(?neao_class), "Generate") ||
            CONTAINS(STR(?neao_class), "Apply")) .

    ?output_file alpaca:filePath ?file_path .

}