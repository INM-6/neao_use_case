PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX alpaca: <http://purl.org/alpaca#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX neao_data: <http://purl.org/neao/data#>
PREFIX neao_steps: <http://purl.org/neao/steps#>
PREFIX neao_base: <http://purl.org/neao/base#>

SELECT DISTINCT ?file_path ?neao_class WHERE {
	?output_file rdf:type alpaca:FileEntity .
    ?output_file (prov:wasDerivedFrom|^prov:hadMember)* ?data .
    ?data rdf:type neao_data:PowerSpectralDensity .

    ?function_execution neao_base:hasOutput ?data .
    ?function_execution rdf:type neao_steps:PowerSpectralDensityAnalysis .
    ?function_execution neao_base:hasInput ?time_series .

    ?time_series (prov:wasDerivedFrom|^prov:hadMember)* ?filtered_time_series .
    ?filter_function neao_base:hasOutput ?filtered_time_series .
    ?filter_function rdf:type ?neao_class .
    ?neao_class rdfs:subClassOf neao_steps:DigitalFiltering .
    FILTER (!CONTAINS(STR(?neao_class), "Filtering")) .

    ?output_file alpaca:filePath ?file_path .
}
ORDER BY ?file_path